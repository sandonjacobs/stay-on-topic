plugins {
    id 'maven-publish'
    id 'com.github.hierynomus.license' version '0.14.0' apply(false)
    id 'pl.allegro.tech.build.axion-release' version '1.9.3'
}

scmVersion {
    tag {
        prefix = 'stay-on-topic'
    }
}

allprojects {
    group 'com.github.sandonjacobs'
    version '1.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    apply plugin: "com.github.hierynomus.license"

    license {
        header = file('../license.txt')
        include "**/*.java"
    }

    project.version = scmVersion.version
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 1.8 // java 8
    targetCompatibility = 1.8

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

