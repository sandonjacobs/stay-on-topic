plugins {
    id 'maven-publish'
    id 'com.github.hierynomus.license' version '0.14.0' apply(false)
    id 'pl.allegro.tech.build.axion-release' version '1.9.3'
}

scmVersion {
    tag {
        prefix = 'stay-on-topic'
    }
}

allprojects {

    buildscript {
        repositories {
            mavenLocal()
        }
    }

    group 'com.github.sandonjacobs'
    version '1.0.0-SNAPSHOT'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    apply plugin: "com.github.hierynomus.license"

    license {
        header = file('../license.txt')
        include "**/*.java"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8 // java 8
    targetCompatibility = 1.8

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = "com.github.sandonjacobs"
                artifactId = project.name
                version = project.version

                from components.java

                pom {
                    developers {
                        developer {
                            name = "Sandon Jacobs"
                            id = "sandonjacobs"
                            email = "sandon.jacobs@gmail.com"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com.com/sandonjacobs/stay-on-topic.git"
                        developerConnection = "scm:git:git://github.com.com/sandonjacobs/stay-on-topic.git"
                        url = "http://github.com.com/sandonjacobs/stay-on-topic"
                    }
                }
            }
        }
    }
}

